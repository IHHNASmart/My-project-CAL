OBJECT Page 50003 Out of Office Requests List
{
  OBJECT-PROPERTIES
  {
    Date=14.06.22;
    Time=18:17:23;
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Out of Office Requests;
               RUS=Заявки на отсутствие];
    SourceTable=Table50002;
    PageType=List;
    CardPageID=Out of Office Request Doc;
    OnInit=BEGIN

               UserSetupTrue := UserSetup.GET(USERID);
               UserSetupHR   := UserSetup."HR administrator";
           END;

    OnOpenPage=BEGIN

                   IF NOT UserSetupHR THEN
                     SETFILTER("Employee No.", UserSetup.Employee);

               END;

    ActionList=ACTIONS
    {
      { 14      ;    ;ActionContainer;
                      CaptionML=ENU=General;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=Start process;
                      CaptionML=[ENU=Start process;
                                 RUS=Начать процесс];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN

                                   //'In process';
                                   IF xRec.Status = xRec.Status::"In process" THEN
                                     EXIT;

                                   IF NOT CheckUserAccess(xRec.Status::"In process") THEN
                                     EXIT;

                                   IF CONFIRM(textSet, FALSE, FORMAT(xRec.Status::"In process")) THEN
                                     SetNewStatus(xRec.Status::"In process",'');

                               END;
                                }
      { 16      ;1   ;Action    ;
                      Name=Approve;
                      CaptionML=[ENU=Approve;
                                 RUS=Утвердить];
                      Promoted=Yes;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN

                                   //'Approved';
                                   IF xRec.Status = xRec.Status::Approved THEN
                                     EXIT;

                                   IF NOT CheckUserAccess(xRec.Status::Approved) THEN
                                     EXIT;

                                   IF CONFIRM(textSet, FALSE, FORMAT(xRec.Status::Approved)) THEN
                                     SetNewStatus(xRec.Status::Approved,'');
                               END;
                                }
      { 17      ;1   ;Action    ;
                      Name=Decline;
                      CaptionML=[ENU=Decline;
                                 RUS=Отклонить];
                      Promoted=Yes;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN

                                   //'Declined';
                                   IF xRec.Status = xRec.Status::Declined THEN
                                     EXIT;

                                   IF NOT CheckUserAccess(xRec.Status::Declined) THEN
                                     EXIT;

                                   IF UserSetupHR THEN BEGIN
                                     CLEAR(dialConfirmationRejected);
                                     dialConfirmationRejected.SetVariablesPage(FORMAT(xRec.Status::Declined));

                                     IF dialConfirmationRejected.RUNMODAL = ACTION::OK THEN BEGIN
                                       SetNewStatus(xRec.Status::Declined, dialConfirmationRejected.GetVariablePage);
                                       //CurrPage.UPDATE;
                                       END;
                                     END
                                   ELSE BEGIN
                                     IF CONFIRM(textSet, FALSE, FORMAT(xRec.Status::Declined)) THEN BEGIN
                                       SetNewStatus(xRec.Status::Declined, STRSUBSTNO(textDeclinedUser, UserSetup.Employee));
                                     END;
                                    END;
                               END;
                                }
      { 18      ;    ;ActionContainer;
                      ActionContainerType=Reports }
      { 19      ;1   ;Action    ;
                      Name=Print Out of Office Day Count;
                      CaptionML=[ENU=Print Out of Office Day Count;
                                 RUS=Печать кол-во дней отсутствия];
                      OnAction=VAR
                                 recordOutOfOffice@1000 : Record 50002;
                                 Filter@1001 : Text;
                               BEGIN

                                   CLEAR(recordOutOfOffice);
                                   CurrPage.SETSELECTIONFILTER(recordOutOfOffice);

                                   IF recordOutOfOffice.FINDSET THEN BEGIN
                                     REPEAT
                                       Filter := Filter + FORMAT(recordOutOfOffice."Entry No") + '|';
                                     UNTIL recordOutOfOffice.NEXT = 0;
                                     Filter := DELSTR(Filter, STRLEN(Filter));
                                     //MESSAGE('%1', Filter);
                                     recordOutOfOffice.RESET;
                                     recordOutOfOffice.SETFILTER("Entry No", Filter);
                                     REPORT.RUNMODAL(REPORT::"Out of Office Day Count", TRUE, TRUE, recordOutOfOffice);
                                     END
                                   ELSE
                                     REPORT.RUNMODAL(REPORT::"Out of Office Day Count", TRUE, TRUE, xRec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                SourceExpr="Entry No" }

    { 4   ;2   ;Field     ;
                SourceExpr="Employee No." }

    { 13  ;2   ;Field     ;
                SourceExpr=EmployeeInitials }

    { 5   ;2   ;Field     ;
                SourceExpr="Start Date" }

    { 6   ;2   ;Field     ;
                SourceExpr="Start Time" }

    { 7   ;2   ;Field     ;
                SourceExpr="End Date" }

    { 8   ;2   ;Field     ;
                SourceExpr="End Time" }

    { 9   ;2   ;Field     ;
                SourceExpr="Reason Code" }

    { 20  ;2   ;Field     ;
                SourceExpr=ReasonName }

    { 10  ;2   ;Field     ;
                SourceExpr=Status }

    { 11  ;2   ;Field     ;
                SourceExpr=Description }

    { 12  ;2   ;Field     ;
                SourceExpr="Rejection reason" }

  }
  CODE
  {
    VAR
      textSet@1000 : TextConst 'ENU=Change status to "%1"?;RUS=Изменить статус на "%1"?';
      dialConfirmationRejected@1001 : Page 50005;
      UserSetup@1004 : Record 91;
      UserSetupTrue@1003 : Boolean;
      UserSetupHR@1002 : Boolean;
      textApproved@1006 : TextConst 'ENU=It is forbidden to change an application in the Approved status!;RUS=Изменять заявку в статусе Approved запрещено!';
      textDeclinedUser@1005 : TextConst 'ENU=Application canceled by user %1;RUS=Заявка отменена пользователем %1';
      reportOutOfOffice@1007 : Report 50000;

    LOCAL PROCEDURE SetNewStatus@1(NewStatus@1000 : Option;TextStatus@1003 : Text[250]);
    VAR
      OutOfOfficeRequest@1001 : Record 50002;
      text001@1002 : TextConst 'ENU=Status set "%1";RUS=Установлен статус "%1"';
    BEGIN

          CLEAR(OutOfOfficeRequest);

          OutOfOfficeRequest.GET(xRec."Entry No");
          OutOfOfficeRequest.Status := NewStatus;

          IF NewStatus = OutOfOfficeRequest.Status::Declined THEN
            OutOfOfficeRequest."Rejection reason" := TextStatus
          ELSE
            OutOfOfficeRequest."Rejection reason" := '';

          OutOfOfficeRequest.MODIFY;

          MESSAGE(text001, OutOfOfficeRequest.Status);

    END;

    LOCAL PROCEDURE SetDefaultEmployee@2();
    BEGIN

        IF NOT UserSetupTrue THEN
          EXIT;

        IF UserSetup.Employee <> '' THEN
          "Employee No." := UserSetup.Employee;
    END;

    LOCAL PROCEDURE CheckUserAccess@8(NewStatus@1000 : Option) AccessGranted : Boolean;
    BEGIN

          IF xRec.Status = xRec.Status::Approved THEN BEGIN
            MESSAGE(textApproved);
            AccessGranted := FALSE;
            EXIT;
          END;

          CASE NewStatus OF
            xRec.Status::"In process",
            xRec.Status::Declined:
              AccessGranted := TRUE;
            xRec.Status::Approved: BEGIN
              IF UserSetupHR THEN
                AccessGranted := TRUE
              ELSE
                BEGIN
                  AccessGranted := FALSE;
                  MESSAGE(textApproved);
                END;
             END;
           END;
    END;

    BEGIN
    END.
  }
}

